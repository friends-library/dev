_default:
  @just --choose

dev-en: lang-en write-cover-css
  @NEXT_PUBLIC_LANG=en pnpm next dev --port 7222

dev-es: lang-es write-cover-css
  @NEXT_PUBLIC_LANG=es pnpm next dev --port 7333

build-en: lang-en write-cover-css web-covers && unweb-covers
  @NEXT_PUBLIC_LANG=en pnpm next build

build-es: lang-es write-cover-css web-covers && unweb-covers
  @NEXT_PUBLIC_LANG=es pnpm next build

build-en-vercel:
  rm -rf app/(friend)/amigo/ app/(friend)/amiga/
  just build-en

build-es-vercel:
  rm -rf app/(friend)/friend/
  rm -rf app/what-early-quakers-believed/
  just build-es

build-analyze:
  @ANALYZE=true just build-en

storybook:
  @cd ../../ && pnpm --filter storybook storybook

test:
  @pnpm vitest . --watch

# helpers

[private]
lang-en:
  @cp ./components/core/Logo{Friends,}.tsx
  @cp ./components/core/Logo{Amigos,Alt}.tsx

[private]
lang-es:
  @cp ./components/core/Logo{Amigos,}.tsx
  @cp ./components/core/Logo{Friends,Alt}.tsx

[private]
web-covers:
  @mv ../../libs-ts/cover-component/src/Logo{Spanish,Icon,}.tsx .
  @cp ../../libs-ts/cover-component/src/Logo{.web,}.tsx
  @cp ../../libs-ts/cover-component/src/LogoSpanish{.web,}.tsx
  @cp ../../libs-ts/cover-component/src/LogoIcon{.web,}.tsx

[private]
unweb-covers:
  @mv ./Logo{,Icon,Spanish}.tsx ../../libs-ts/cover-component/src/

[private]
write-cover-css:
  @pnpm ts-node ./lib/ssg/cover-static-css.ts
