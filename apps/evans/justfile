_default:
  @just --choose

start-en: write-cover-css
  #!/usr/bin/env bash
  export GATSBY_LANG=en GATSBY_PORT=9222 GATSBY_ALT_PORT=9333
  pnpm gatsby develop --port $GATSBY_PORT

start-es: write-cover-css
  #!/usr/bin/env bash
  export GATSBY_LANG=es GATSBY_PORT=9333 GATSBY_ALT_PORT=9222
  pnpm gatsby develop --port $GATSBY_PORT

build-en: write-cover-css && post-build
  @GATSBY_LANG=en pnpm gatsby build

build-es: write-cover-css && post-build
  @GATSBY_LANG=es pnpm gatsby build

analyze: write-cover-css && post-build
  @ANALYZE_BUNDLE_SIZE=true GATSBY_LANG=en pnpm gatsby build

codegen:
  @mv src/ssg/schema.{graphql,txt} > /dev/null ; true
  @pnpm flp-gql-codegen --convert-dates-to-string --remove-custom-scalars
  @mv src/ssg/schema.{txt,graphql}

clean:
  @rm ./src/ssg/.dpc-cache.json
  @rm ./src/css/cover.css
  @pnpm gatsby clean

# used to create dist/ssg/query.js - for github republish action
publish-query:
  @pnpm tsc src/ssg/query.ts --skipLibCheck --target esnext --moduleResolution node --outDir dist/ --declaration

prettier:
  @just ../../prettier

# helpers

[private]
post-build:
  @cp ./_redirects ./public
  @cp ./_headers ./public

[private]
write-cover-css:
  @pnpm ts-node ./src/ssg/cover-static-css.ts
